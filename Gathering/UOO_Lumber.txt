/////////////////////////////////////////////
// UO Steam Lumber Macro
// for Outlands
//
// * just chops wood, nothing else
//
// Version 1.0
// -----------
// First release
//
// Version 1.1
// -----------
// Added PK detection and warnings. To use this
// activate your tracking and set to [REDS]
//
// Version 1.2
// -----------
// Copied logic from Miner macro
// Will now recall if it detects reds
// Sped up script over previous versions
//
// Version 1.3
// -----------
// Removed captcha detection and interaction
//
// Version 1.4
// -----------
// Well.. looks like "using" logs to convert them
// to boards unequips your axe. So just do that 
// manually if you need it.
//
// How to use:
// -----------
// 1. Put Hatchets in pack
// 2. If you want to recall on seeing reds:
//    a. Enable tracking
//    b. Stock recall regs
//    c. Record and save a recall macro named 'recall'
// 3. Run script
//
/////////////////////////////////////////////
sysmsg "UO Outlands Lumberjack script" 88
sysmsg "Version 1.4" 88
@clearjournal
@useobject 'backpack'
pause 1000
//
// Clear hands
//
clearhands 'both'
pause 1000
//
// Whistle timer
//
removetimer 'whistle'
createtimer 'whistle'
//
// Clear alias (possibly other macros)
//
@unsetalias 'Hatchet'
//
// While alive
//
while hits > 0
  //
  // If still too heavy, need player help
  //
  while diffweight < 10
    headmsg 'I am too heavy, please unload me' '23'
    pause 2000
  endwhile
  //
  // Check holding Hatchet
  //
  if not @findlayer 'self' 2
    pause 1000
    @unsetalias 'Hatchet'
  endif
  //
  // Equip Hatchet or stop with msg
  //
  if not findalias 'Hatchet'
    if @findtype 0xf44 'any' 'backpack'
      @setalias 'Hatchet' 'found'
      // This equips it
      useobject 'Hatchet'
      pause 1000
    else
      headmsg 'No Hatchet found' '55' 'self'
      stop
    endif
  endif
  @canceltarget
  //
  // Chop wood
  //
  @clearjournal
  useobject 'Hatchet'
  waitfortarget 1000
  target! 'self'
  pause 100
  // Wait for results
  for 40
    if @injournal 'enough wood here' 'system'
      headmsg 'Find a new spot' '43'
      pause 1000
    elseif @injournal 'You chop' 'system'
      break
    elseif @injournal "You loosen" "system"
      break
    elseif @injournal "You find" "system"
      msg "well look at that..."
      break
    elseif @injournal "harvestable" "system"
      headmsg 'Find a new spot' '43'
      pause 1000
      break
    elseif not @findlayer 'self' 2
      headmsg 'NO AXE EQUIPPED' '23'
      break
    else
      pause 100
    endif
  endfor
  //
  // Whistle
  //
  if timer 'whistle' > 20000
    emotemsg 'whistles'
    settimer 'whistle' 0
  endif
  //
  // PK Detect
  //
  if @injournal "now tracking" 'system'
    headmsg "PK PK PK!" 22
    playmacro 'recall'
    stop
  endif
endwhile
