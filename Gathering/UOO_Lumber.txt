/////////////////////////////////////////////
// UO Steam Lumber Macro
// for Outlands
//
// * just chops wood, nothing else
// 
// Version 1.0
// -----------
// First release
//
// Version 1.1
// -----------
// Added PK detection and warnings. To use this
// activate your tracking and set to [REDS]
//
// Version 1.2
// -----------
// Copied logic from Miner macro
// Will now recall if it detects reds
// Sped up script over previous versions
//
// How to use:
// -----------
// 1. Put Hatchets in pack
// 2. If you want to recall on seeing reds:
//    a. Enable tracking
//    b. Stock recall regs
//    c. Record and save a recall macro named 'recall'
// 3. Run script
//
/////////////////////////////////////////////
sysmsg "UO Outlands Lumberjack script" 88
sysmsg "Version 1.2" 88
@useobject 'backpack'
pause 1000
//
// Clear hands
//
clearhands 'both'
pause 1000
//
// Whistle timer
//
removetimer 'whistle'
createtimer 'whistle'
//
// Clear alias (possibly other macros)
//
@unsetalias 'Hatchet'
//
// Create Messages list
//
@removelist 'Messages'
@createlist 'Messages'
//
// While alive
//
while hits > 0
  @clearjournal
  //
  // Gump detect
  //
  if @ingump 'any' 'Click when complete'
    headmsg "Answer gump" 55
    playsound 'buzzer.wav'
    pause 10000
    continue
  endif
  //
  // Convert to boards
  //
  if diffweight < 30
    @usetype '0x1bdd'   
    pause 2000
  endif
  //
  // If still too heavy, need player help
  //
  while diffweight < 10
    headmsg 'I am too heavy, please unload me' '23'
    pause 2000
  endwhile
  //
  // Check holding Hatchet
  //
  if not @findlayer 'self' 2
    pause 1000
    @unsetalias 'Hatchet'
  endif
  //
  // Equip Hatchet or stop with msg
  //
  if not findalias 'Hatchet'
    if @findtype 0xf44 'any' 'backpack'
      @setalias 'Hatchet' 'found'
      // This equips it
      useobject 'Hatchet'
      pause 1000
    else
      headmsg 'No Hatchet found' '55' 'self'
      stop
    endif
  endif
  @canceltarget
  //
  // Chop wood
  //
  useobject 'Hatchet'
  waitfortarget 1000
  target! 'self'
  pause 100
  // Wait for results
  for 21
    if @injournal 'enough wood here' 'system'
      headmsg 'Find a new spot' '43'
      pause 1000
    elseif @injournal 'You chop' 'system'
      break
    elseif @injournal "You loosen" "system"
      break
    elseif @injournal "You find" "system"
      msg "well look at that..."
      break
    elseif @injournal "harvestable" "system"
      headmsg 'Find a new spot' '43'
      pause 1000
      break
    elseif not @findlayer 'self' 2
      headmsg 'NO AXE EQUIPPED' '23'
      break
    else
      pause 100
    endif
  endfor
  //
  // Whistle
  //
  if timer 'whistle' > 20000
    emotemsg 'whistles'
    settimer 'whistle' 0
  endif
  //
  // PK Detect
  //
  if @injournal "now tracking" 'system'
    headmsg "PK PK PK!" 22
    playmacro 'Recall'
    stop
  endif
endwhile
