/////////////////////////////////////////////
// UO Steam Alchemy Script
// for Outlands
// by Jewele
//
// Script to make alchemy potions of your choice
// pulling from a container and placing filled kegs
// back into same. You can use this to train alchemy
// if you wish, or just to make potion kegs.
//
// How to use?
// -----------
// 1) Modify script for potion you want to use
// 2) Place empty kegs and reagents in a container (or bank bag)
// 3) Stock backpack with pestles (lots!) and 1 bottle
// 4) Run script
//
// Sounds
// ------
// ..\UOSteam\Sounds\Chime.wav
// ..\UOSteam\Sounds\UhOh.wav
//
// Version 1.0
// -----------
// First release! yay!
//
// Version 1.1
// -----------
// Fixed issue with making Greater Cures
//
// Version 1.2
// -----------
// Sped up script with excellent suggestions from Luckbox777!
//
// Version 1.3
// -----------
// Moved bank calls before alias sets
//
// Version 1.4
// -----------
// Removed all stops once script starts to make potions
//
// Technically, you dont have to use this at a bank. I figured
// most would. You can use a chest in your house as well. Just
// select that as your reg source :)
//
// Version 1.5
// -----------
// * Fixed some issues reported from users and my own testing
// * Added sound file and pause when a keg is full in case you
//   want to catch the script and stop it.
// * Added 'uhoh' sound file invocation to let you know of a problem
// * No longer uses make last
// * Added weight check 
//
// UOSTEAM COPY-PASTE BELOW THIS LINE
/////////////////////////////////////////////

sysmsg "Alchemy Script" 88
sysmsg "Version 1.5" 88
//
// Open backpack
//
@useobject 'backpack'
pause 1000

//
// Open bank
// 
msg "bank"
pause 1000
msg "bank"
pause 1000

//
// Set alias'
//
if not findalias 'kegbag'
    sysmsg "Select container (or bag in bank) with empty kegs, full kegs will be placed here" 88
    promptalias 'kegbag'
endif
if not findalias 'regbag'
    sysmsg "Select container (or bag in bank) with reagents," 88
    promptalias 'regbag'
endif

// ****************************
// ** MODIFY THIS FOR POTION **
// ****************************
// Add exact name of potion you want to make
// No need to add the 'Potion' part of name.
//
@removelist 'potion'
@createlist 'potion'
@pushlist 'potion' 'Poison'
//
// ****************
// ** END MODIFY **
// ****************
//
// Resolve which reg type we need
//
@removelist 'regs'
@createlist 'regs'
if @inlist 'potion' 'Lesser Heal'
    pushlist 'regs' 0xf85 // ginseng
elseif @inlist 'potion' 'Heal'
    pushlist 'regs' 0xf85 // ginseng
elseif @inlist 'potion' 'Greater Heal'
    pushlist 'regs' 0xf85 // ginseng
elseif @inlist 'potion' 'Lesser Cure'
    pushlist 'regs' 0xf84 // garlic
elseif @inlist 'potion' 'Cure'
    pushlist 'regs' 0xf84 // garlic
elseif @inlist 'potion' 'Greater Cure'
    pushlist 'regs' 0xf84 // garlic
elseif @inlist 'potion' 'Refresh'
    pushlist 'regs' 0xf7a // pearl
elseif @inlist 'potion' 'Total Refresh'
    pushlist 'regs' 0xf7a // pearl
elseif @inlist 'potion' 'Strength'
    pushlist 'regs' 0xf86 // root
elseif @inlist 'potion' 'Greater Strength'
    pushlist 'regs' 0xf86 // root
elseif @inlist 'potion' 'Agility'
    pushlist 'regs' 0xf7b // moss
elseif @inlist 'potion' 'Greater Agility'
    pushlist 'regs' 0xf7b // moss
elseif @inlist 'potion' 'Lesser Magic Resist'
    pushlist 'regs' 0xf8d // silk
elseif @inlist 'potion' 'Magic Resist'
    pushlist 'regs' 0xf8d // silk
elseif @inlist 'potion' 'Greater Magic Resist'
    pushlist 'regs' 0xf8d // silk
elseif @inlist 'potion' 'Lesser Poison'
    pushlist 'regs' 0xf88 // shade
elseif @inlist 'potion' 'Poison'
    pushlist 'regs' 0xf88 // shade
elseif @inlist 'potion' 'Greater Poison'
    pushlist 'regs' 0xf88 // shade
elseif @inlist 'potion' 'Deadly Poison'
    pushlist 'regs' 0xf88 // shade
elseif @inlist 'potion' 'Lethal Poison'
    pushlist 'regs' 0xf88 // shade
elseif @inlist 'potion' 'Lesser Explosion'
    pushlist 'regs' 0xf8c // ash
elseif @inlist 'potion' 'Explosion'
    pushlist 'regs' 0xf8c // ash
elseif @inlist 'potion' 'Greater Explosion'
    pushlist 'regs' 0xf8c // ash
else
    playsound 'uhoh.wav'
    headmsg "Unable to figure out potion type you requested"
    stop
endif

//
// Potions list
// 
@removelist 'potions'
@createlist 'potions'
pushlist 'potions' 0xf0c // heal
pushlist 'potions' 0xf07 // cure
pushlist 'potions' 0xf06 // resist
pushlist 'potions' 0xf0a // poison
pushlist 'potions' 0xf08 // agility
pushlist 'potions' 0xf0b // refresh
pushlist 'potions' 0xf0a // poison
pushlist 'potions' 0xf0d // explo
pushlist 'potions' 0xf09 // strength

//
// Check no potions in pack
// 
sysmsg "pack potion check" 16
for 0 to 'potions'
    if @findtype potions[] 'any' 'backpack'
        headmsg "Please remove all potions from pack before starting" 88
	    playsound 'uhoh.wav'
        stop
    endif
endfor

//
// Check for 1 empty bottle
//
sysmsg "empty bottle check" 16
if counttype 0xf0e 0 'backpack' != 1
    headmsg "I need EXACTLY 1 empty bottle" 88
    playsound 'uhoh.wav'
    stop
endif

@clearjournal

//
// Main loop
//
while not dead
    
	sysmsg "loop start" 16

    //
    // Check for a keg or transfer empty one
    //
    if not @findtype 0x1940 'any' 'backpack'
        movetype 0x1940 'kegbag' 'backpack' 0 0 0 0 1
        pause 2000
    endif
    
    //
    // Verify keg. Server save on restock may occur
    // so throw message, wait, and start again
    //
    if not @findtype 0x1940 'any' 'backpack'
        headmsg "I need a keg to continue!" 88
        pause 5000
        continue
    endif
    
    //
    // Check if we need to pour into keg
    //
    if not @findtype 0xf0e 0 'backpack'
        headmsg "Pouring into keg" 88
        @unsetalias 'keg'
        @findtype 0x1940 'any' 'backpack'
        @setalias 'keg' 'found'
        for 0 to 'potions'
            if @findtype potions[] 'any' 'backpack'
                moveitem 'found' 'keg'
                pause 3000
                break
            endif
        endfor
    endif
    
    //
    // Check for empty bottle
    //
    if not @findtype 0xf0e 0 'backpack'
        headmsg "I need an empty bottle" 88
        playsound 'uhoh.wav'
	    pause 5000
        continue
    endif
    
    //
    // Verify pestle
    //
    if not @findtype 0xe9b 'any' 'backpack'
        headmsg "I need mortar and pestles to continue!" 88
        playsound 'uhoh.wav'
	    pause 5000
        continue
    endif

    //
    // Restock regs
    //
    if @counttype regs[0] 'any' 'backpack' < 200
        sysmsg "restocking reagents" 88
        msg "bank"
        pause 500
        movetype regs[0] 'regbag' 'backpack' 0 0 0 'any' 200
        pause 2000
    endif
    
    //
    // Verify regs. Server save on restock may occur
    // so throw message, wait, and start again
    //
    if @counttype regs[0] 'any' 'backpack' < 200
        headmsg "I need more regs to continue!" 88
        pause 5000
        playsound 'uhoh.wav'
	    continue
    endif

	//
	// Check weight
	//
	if diffweight < 5
		headmsg 'I am too heavy to make anything, please unload me' 88
		playsound 'uhoh.wav'
		pause 5000
		continue
	endif

    //
    // Use pestle
    // 
    @findtype 0xe9b 'any' 'backpack'
    @useobject 'found'
    waitforgump 0x38920abd 5000

	//
	// Make
	//
	if @inlist 'potion' 'Lesser Heal'
	    replygump 0x38920abd 100
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 200
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Heal'
	    replygump 0x38920abd 100
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 201
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Greater Heal'
	    replygump 0x38920abd 100
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 202
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Lesser Cure'
	    replygump 0x38920abd 101
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 200
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Cure'
	    replygump 0x38920abd 101
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 201
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Greater Cure'
	    replygump 0x38920abd 101
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 202
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Refresh'
	    replygump 0x38920abd 102
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 200
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Total Refresh'
	    replygump 0x38920abd 102
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 201
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Strength'
	    replygump 0x38920abd 103
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 200
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Greater Strength'
	    replygump 0x38920abd 103
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 201
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Agility'
	    replygump 0x38920abd 104
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 200
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Greater Agility'
	    replygump 0x38920abd 104
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 201
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Lesser Magic Resist'
	    replygump 0x38920abd 105
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 200
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Magic Resist'
	    replygump 0x38920abd 105
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 201
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Greater Magic Resist'
	    replygump 0x38920abd 105
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 202
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Lesser Poison'
	    replygump 0x38920abd 106
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 200
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Poison'
	    replygump 0x38920abd 106
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 201
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Greater Poison'
	    replygump 0x38920abd 106
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 202
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Deadly Poison'
	    replygump 0x38920abd 106
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 203
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Lethal Poison'
	    replygump 0x38920abd 106
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 204
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Lesser Explosion'
	    replygump 0x38920abd 107
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 200
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Explosion'
	    replygump 0x38920abd 107
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 201
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	elseif @inlist 'potion' 'Greater Explosion'
	    replygump 0x38920abd 107
	    waitforgump 0x38920abd 5000 
	    replygump 0x38920abd 202
		waitforgump 0x38920abd 15000
		waitforgump 0x38920abd 15000
	else
	    headmsg "Unable to figure out potion type you requested" 88
	    pause 5000
	    continue
	endif

	//
	// Pause to get potion transfer automatically
	//
	pause 1500

    //
    // Check if we need to pour into keg
    //
    if not @findtype 0xf0e 0 'backpack'
        headmsg "Pouring into keg" 71
        @unsetalias 'keg'
        @findtype 0x1940 'any' 'backpack'
        @setalias 'keg' 'found'
        for 0 to 'potions'
            if @findtype potions[] 'any' 'backpack'
                moveitem 'found' 'keg'
                pause 3000
                break
            endif
        endfor
    endif

    //
    // Check if keg full
    //
    if @injournal 'hold any more' 'system'
        headmsg "Keg full" 88  
        sysmsg "Keg full, continuing in 10 seconds" 88  
        msg "bank"
        pause 500
        movetype 0x1940 'backpack' 'kegbag' 0 0 0 'any' 1
        playsound 'chime.wav'
        pause 10000
    endif

    //
    // World save check
    //
    if @injournal 'world will save' 'system'
      sysmsg 'pausing for world save'
      pause 30000
    endif

    @clearjournal

endwhile