/////////////////////////////////////////////
// UO Steam Society Macro for Carpenter
// for Outlands
// by Jewele
//
// Crafts a certain number of items
// required for a society then stops, 
// alerting you when its done.
//
// This isn't fully AFK - its designed to be
// monitwoodd. So semi-afk :)
//
// How to use:
// ===========
// 1) Stock pack with Saws
// 2) Set crafting menu to metal you need
// 3) Set recycle option to 'RECYCLE SINGLE ITEM'
// 4) Craft 1 item you need for job (sets make last)
// 5) Add the graphic ID to the list (below)
// 6) Modify the wood color list code (below)
// 7) Run script
// 8) Wait for done alert
//
// Version 1.0
// -----------
// First release! yay!
//
// UOSTEAM COPY-PASTE BELOW THIS LINE
/////////////////////////////////////////////
@removelist 'wood'
@createlist 'wood'
@removelist 'graphicid'
@createlist 'graphicid'
// MODIFY START --------->
//
//
// Add wood color for job
pushlist 'wood' 2213      // goldenwood

// Add item graphic id for job item
pushlist 'graphicid' 0x13f8
//
//
// <---------- MODIFY END
//
// Open backpack
//
@useobject 'backpack'
pause 1000
msg "bank"
pause 1000
useskill 'hiding'
//
// Set alias
//
if not @findalias 'woodbox'
  sysmsg "Select chest with materials"
  promptalias 'woodbox'
endif
//
// Move all boards from pack to woodbox (reset)
//
while @findtype 0x1bd7 'any' 'backpack'
  sysmsg "dumping"
  moveitem  'found' 'woodbox'
  pause 500
endwhile
//
// Loop until its done
//
@clearjournal
while not @injournal "completed a society job" 'System'
  //
  // Check weight
  //
  if diffweight < 10
    headmsg 'I am too heavy, please unload me' '88'
    playsound 'uhoh.wav'
    pause 5000
    continue
  endif
  //
  // Check or restock boards
  //
  if @counttype 0x1bd7 wood[0] 'backpack' <  25
    sysmsg "restocking"
    useobject 'woodbox'
    pause 1000
    movetype 0x1bd7 'woodbox' 'backpack' 0 0 0 wood[0] 500
    pause 1000
  endif
  //
  // Verify boards
  //
  if @counttype 0x1bd7 wood[0] 'backpack' <  25
    playsound 'uhoh.wav'
    headmsg 'I cannot restock boards!' '88'
    pause 5000
    continue
  endif
  //
  // Check saws
  //
  if not @findtype 0x1034 'any' 'backpack'
    headmsg 'I have no saws!' 66
    playsound 'uhoh.wav'
    pause 5000
    continue
  endif
  //
  // Use 3x = sometimes server doesnt respond
  //
  useobject 'found'
  pause 100
  useobject 'found'
  pause 100
  useobject 'found'
  pause 100
  //
  // Craft last
  //
  waitforgump 0x38920abd 5000
  replygump 0x38920abd 7
  // craft response
  waitforgump 0x38920abd 5000
  // then dismiss
  replygump 0x38920abd 0
  // then wait for item
  waitforgump 0x38920abd 10000
  //
  // Recycle
  //
  while @findtype graphicid[0] 'any' 'backpack'
    @usetype 0x1034
    waitforgump 0x38920abd 15000
    replygump 0x38920abd 3
    waitfortarget 5000
    target! 'found'
    pause 1000
  endwhile
endwhile

//
// Burn off the extras if found
//
while @findtype graphicid[0] 'any' 'backpack'
  @usetype 0x1034
  waitforgump 0x38920abd 15000
  replygump 0x38920abd 3
  waitfortarget 5000
  target! 'found'
  pause 1000
endwhile

playsound 'cymbals.wav'
headmsg "All done!" 55
