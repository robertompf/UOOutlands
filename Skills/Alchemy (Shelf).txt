/////////////////////////////////////////////
// UO Steam Training Script
// for Outlands
// by Jewele
//
// Alchemy 50-120 trainer, using a shelf in your home. Uses
// a combination of Greater Heal and Poisons to level. 
//
// How to use?
// -----------
// 1) Stock your shelf with :
//    -Nightshade
//    -Mortars and Pestles (optional)
// 2) Configure a Restock profile for:
//    -5 Mortar and Pestle (optional)
//    -300+ Nightshade
//    -300+ Ginseng
// 3) Place a secure box near your working area with:
//    - Empty kegs
//    - Pestles (optional)
// 4) Put exactly 1 empty bottle in your pack
// 5) Run script
//
// Notes:
// ------
// You can decide how you want your potions stored. If you
// want them in the shelf, you need to add empty bottles
// to shelf to accomodate dropping kegs onto it. If you
// do not have enough bottles, then the kegs will be placed
// back in your box when full.
//
// You can keep pestles in your shelf profile and pull them
// that way, OR, you can put them in your box. Sometimes
// buying 50 of them at the Mage shop and dumping them into
// a box is just easier. Works either way.
//
// Script has been calibrated to make any kind of potion ~
// you can further adjust this to your own needs. Some folks
// make poison all the way, I prefer to mix it up.
//
// Scripts which just use 'make last' are going to be faster 
// but we cannot use that here, due to changing skill levels
// and skill targets.
//
// Script not running?
//
//    Clear your aliases. Go to Macros-Active Objects, and clear 
//    all Active Objects. 
//
// Script buggy?
//
//    DO NOT share profiles between characters, it will confuse
//    the aliases.
//
// UOSTEAM COPY-PASTE BELOW THIS LINE
/////////////////////////////////////////////
sysmsg "Alchemy Trainer Script" 88
@useobject 'backpack'
pause 1000
@removelist 'potion'
@createlist 'potion'
@removelist 'regs'
@createlist 'regs'
//
// Potions list
//
@removelist 'potions'
@createlist 'potions'
pushlist 'potions' 0xf0c // heal
pushlist 'potions' 0xf07 // cure
pushlist 'potions' 0xf06 // resist
pushlist 'potions' 0xf0a // poison
pushlist 'potions' 0xf08 // agility
pushlist 'potions' 0xf0b // refresh
pushlist 'potions' 0xf0a // poison
pushlist 'potions' 0xf0d // explo
pushlist 'potions' 0xf09 // strength
//
// Check no potions in pack
//
for 0 to 'potions'
  if @findtype potions[] 'any' 'backpack'
    headmsg "Please remove all potions from pack before starting" 91
    stop
  endif
endfor
//
// Check for 1 empty bottle
//
if counttype 0xf0e 0 'backpack' != 1
  headmsg "I need EXACTLY 1 empty bottle" 44
  sysmsg "I need EXACTLY 1 empty bottle" 44
  stop
endif
//
// Set alias'
//
if not findalias 'box'
  sysmsg "Select a box with empty kegs"
  promptalias 'box'
endif
if not findalias 'shelf'
  sysmsg "Select shelf"
  promptalias 'shelf'
endif
@clearjournal
@canceltarget
//
// Open box
//
@useobject 'box'
//
// Main loop
//
while not dead
  //
  // Determine what we have to make
  // according to skill. Feel free to
  // adjust these values for the potions
  // you want to keep in your shelf.
  //
  @clearlist 'potion'
  @clearlist 'regs'
  if skill 'Alchemy' < 50
    sysmsg "GO GET 50 ALCHEMY!" 32
    stop
  elseif skill 'Alchemy' < 65
    sysmsg "Making Poison" 75
    pushlist 'potion' 'Poison'
    pushlist 'regs' 0xf88 // shade
  elseif skill 'Alchemy' < 90
    sysmsg "Making Greater Heal" 75
    pushlist 'potion' 'Greater Heal'
    pushlist 'regs' 0xf85 // ginseng
  elseif skill 'Alchemy' < 115
    sysmsg "Making Deadly Poison" 75
    pushlist 'potion' 'Deadly Poison'
    pushlist 'regs' 0xf88 // shade
  elseif skill 'Alchemy' < 120
    sysmsg "Making Lethal Poison" 75
    pushlist 'potion' 'Lethal Poison'
    pushlist 'regs' 0xf88 // shade
  elseif skill 'Alchemy' == 120
    headmsg "DONE!" 66
    stop
  endif
  //
  // Check for a keg or transfer empty one
  //
  if not @findtype 0x1940 'any' 'backpack'
    movetype 0x1940 'box' 'backpack' 0 0 0 0 1
    pause 2000
  endif
  //
  // Verify keg. Server save on restock may occur
  // so throw message, wait, and start again
  //
  if not @findtype 0x1940 'any' 'backpack'
    headmsg "I need a keg to continue!" 91
    pause 5000
    continue
  endif
  //
  // Check if we need to pour into keg
  //
  if not @findtype 0xf0e 0 'backpack'
    headmsg "Pouring into keg" 91
    @unsetalias 'keg'
    @findtype 0x1940 'any' 'backpack'
    @setalias 'keg' 'found'
    for 0 to 'potions'
      if @findtype potions[] 'any' 'backpack'
        moveitem 'found' 'keg'
        pause 1500
        break
      endif
    endfor
  endif
  //
  // Check for empty bottle
  //
  if not @findtype 0xf0e 0 'backpack'
    headmsg "I need an empty bottle" 91
    pause 5000
    continue
  endif
  //
  // Check for a keg or transfer empty one
  //
  if not @findtype 0x1940 'any' 'backpack'
    movetype 0x1940 'kegbag' 'backpack' 0 0 0 0 1
    pause 2000
  endif
  //
  // Verify keg. Server save on restock may occur
  // so throw message, wait, and start again
  //
  if not @findtype 0x1940 'any' 'backpack'
    headmsg "I need a keg to continue!" 91
    pause 5000
    continue
  endif
  //
  // Check for a pestle or transfer one
  // from box.
  //
  if not @findtype 0xe9b 'any' 'backpack'
    sysmsg "Restocking pestles from box" 44
    movetype 0xe9b 'box' 'backpack' 0 0 0 0 1
    pause 2000
  endif
  //
  // Check for a pestle or transfer one
  // from shelf.
  //
  if not @findtype 0xe9b 'any' 'backpack'
    sysmsg "Restocking pestles from shelf" 44
    waitforcontext 'shelf' 1 5000
    pause 2000
  endif
  //
  // Verify pestle
  //
  if not @findtype 0xe9b 'any' 'backpack'
    headmsg "I need mortar and pestles to continue!" 91
    pause 5000
    continue
  endif
  //
  // Restock regs
  //
  if @counttype regs[0] 'any' 'backpack' < 200
    sysmsg "Restocking reagents" 44
    waitforcontext 'shelf' 1 5000
    pause 2000
  endif
  //
  // Verify regs.
  //
  if not @counttype regs[0] 'any' 'backpack' > 199
    headmsg "I need regs to continue!" 91
    pause 5000
    continue
  endif
  //
  // Use pestle
  //
  @findtype 0xe9b 'any' 'backpack'
  @useobject 'found'
  waitforgump 0x38920abd 5000
  //
  // ** Potion select **
  //
  if @inlist 'potion' 'Lesser Heal'
    replygump 0x38920abd 100
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 200
  elseif @inlist 'potion' 'Heal'
    replygump 0x38920abd 100
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 201
  elseif @inlist 'potion' 'Greater Heal'
    replygump 0x38920abd 100
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 202
  elseif @inlist 'potion' 'Lesser Cure'
    replygump 0x38920abd 101
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 200
  elseif @inlist 'potion' 'Cure'
    replygump 0x38920abd 101
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 201
  elseif @inlist 'potion' 'Greater Cure'
    replygump 0x38920abd 101
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 202
  elseif @inlist 'potion' 'Refresh'
    replygump 0x38920abd 102
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 200
  elseif @inlist 'potion' 'Total Refresh'
    replygump 0x38920abd 102
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 201
  elseif @inlist 'potion' 'Strength'
    replygump 0x38920abd 103
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 200
  elseif @inlist 'potion' 'Greater Strength'
    replygump 0x38920abd 103
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 201
  elseif @inlist 'potion' 'Agility'
    replygump 0x38920abd 104
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 200
  elseif @inlist 'potion' 'Greater Agility'
    replygump 0x38920abd 104
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 201
  elseif @inlist 'potion' 'Lesser Magic Resist'
    replygump 0x38920abd 105
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 200
  elseif @inlist 'potion' 'Magic Resist'
    replygump 0x38920abd 105
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 201
  elseif @inlist 'potion' 'Greater Magic Resist'
    replygump 0x38920abd 105
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 202
  elseif @inlist 'potion' 'Lesser Poison'
    replygump 0x38920abd 106
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 200
  elseif @inlist 'potion' 'Poison'
    replygump 0x38920abd 106
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 201
  elseif @inlist 'potion' 'Greater Poison'
    replygump 0x38920abd 106
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 202
  elseif @inlist 'potion' 'Deadly Poison'
    replygump 0x38920abd 106
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 203
  elseif @inlist 'potion' 'Lethal Poison'
    replygump 0x38920abd 106
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 204
  elseif @inlist 'potion' 'Lesser Explosion'
    replygump 0x38920abd 107
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 200
  elseif @inlist 'potion' 'Explosion'
    replygump 0x38920abd 107
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 201
  elseif @inlist 'potion' 'Greater Explosion'
    replygump 0x38920abd 107
    waitforgump 0x38920abd 5000
    replygump 0x38920abd 202
  else
    headmsg "Unable to figure out potion type you requested" 91
    pause 5000
    continue
  endif
  //
  // wait for crafting result
  //
  @removetimer 'craft'
  @createtimer 'craft'
  while timer 'craft' < 5000
    if @ingump '0x38920abd' 'You pour'
      break
    endif
    if @ingump '0x38920abd' 'You create'
      break
    endif
    if @ingump '0x38920abd' 'You fail'
      break
    endif
    if @ingump 'any' 'resources required'
      break
    endif
    if @ingump 'any' 'worn out'
      break
    endif
    pause 100
  endwhile
  //
  // Check if we need to pour into keg
  // (Empty bottle will tell us)
  //
  if not @findtype 0xf0e 0 'backpack'
    headmsg "Pouring into keg" 91
    @unsetalias 'keg'
    // leave this set to 'any'
    if @findtype 0x1940 'any' 'backpack'
      @setalias 'keg' 'found'
      for 0 to 'potions'
        if @findtype potions[] 'any' 'backpack'
          moveitem 'found' 'keg'
          pause 1500
          break
        endif
      endfor
    endif
  endif
  //
  // Check if keg full or we attempting to mix
  //
  if @injournal 'hold any more' 'system' or @injournal 'mix' 'system'
    headmsg "Keg full" 91
    sysmsg "Dropping keg on shelf" 81
    movetype 0x1940 'backpack' 'shelf' 0 0 0 'any' 1
    pause 2500
    //
    // Verify potion pull
    //
    if not @findtype 0xf0e 0 'backpack'
      sysmsg "Shelf has no empty bottles, moving to box" 81
      movetype 0x1940 'backpack' 'box' 0 0 0 'any' 1
      pause 2500
    endif
  endif
  //
  // World save check
  //
  if @injournal 'world will save' 'system'
    sysmsg 'pausing for world save'
    pause 30000
  endif
  @clearjournal
  pause 500
endwhile