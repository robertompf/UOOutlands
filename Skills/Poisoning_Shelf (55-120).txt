/////////////////////////////////////////////
// UO Steam Poisoning Trainer 55-120 (from Shelf)
// for Outlands
// by Jewele
//
// I didn't like some of the other shelf poisoning macros I saw
// so I rewrote them into this one - its much simpler and just
// as effective.
//
// Make SURE you have 100 str. Server forces a pause after poisoning
// which causes a delay before you can cure anything. You need to be
// able to take a *tic* of poison before cures will kick in! The reason
// you need 100 STR is if you are doing Lethal - you need to be able
// to take 1-2 tics before dying.
//
// Sounds
// ------
// ..\UOSteam\Sounds\UhOh.wav
// ..\UOSteam\Sounds\Chimes.wav
//
// How to use?
// -----------
// 1) Start at home next to shelf
// 2) Put a _modified_ kryss in your pack
//    * you can poison it
//    * you can aspect it
// 3) Stock shelf with poison, cure and heal
// 4) Run script
//
// Wiki Info:
// ----------
// Lesser Poison: 0-40 Poisoning
// Regular Poison: 35-60 Poisoning
// Greater Poison: 55-80 Poisoning
// Deadly Poison: 75-100 Poisoning
// Lethal Poison: 95-120 Poisoning
//
// Version 1.0
// -----------
// First release! yay!
//
// Version 1.1
// -----------
// Added sound files
// Added weight check
// Just uses heal pots to heal
// Added pause for skill cooldown
//
// UOSTEAM COPY-PASTE BELOW THIS LINE
/////////////////////////////////////////////
sysmsg "Poisoning Shelf Script" 73
sysmsg "Version 1.1" 73
@clearjournal
//
// Open backpack
//
@useobject 'backpack'
pause 1000
//
// Set shelf alias'
//
if not findalias 'shelf'
  sysmsg "Select your shelf"
  promptalias 'shelf'
endif
//
// Skill check
//
if skill 'poisoning' < 55
  headmsg 'Train With Poison Potions til 55' 21
  stop
endif
//
// Actions list
//
@removelist 'actions'
@createlist 'actions'
pushlist 'actions' 'restock'
while not dead
  //
  // Weight check (empty bottles)
  //
  if diffweight < 10
    sysmsg "Dumping bottles" 88
    waitforcontext 'shelf' 0 5000
    waitfortarget 5000
    target! 'self'
    pause 5000
  endif
  //
  // Weight check for other reasons
  //
  if diffweight < 10
    headmsg "Im too heavy to be pulling items!" 32
    playsound 'uhoh.wav'
    stop
  endif
  //
  // 120
  //
  if skill 'poisoning' == 120
    playsound 'cymbals.wav'
    headmsg 'Done!' 67
    stop
  endif
  //
  // Check : Restock
  //
  if not @inlist! 'actions' 'restock'
    //
    // cures
    //
    if @counttype 0xf07 'any' 'backpack' < 10
      pushlist! 'actions' 'restock'
      continue
    endif
    //
    // heals
    //
    if @counttype 0xf0c 'any' 'backpack' < 5
      pushlist! 'actions' 'restock'
      continue
    endif
    //
    // poison
    //
    if @counttype 0xf0a 'any' 'backpack' < 1
      pushlist! 'actions' 'restock'
      continue
    endif
  endif
  //
  // Restock
  //
  if @inlist! 'actions' 'restock'
    @clearlist 'actions'
    sysmsg "restocking" 67
    useobject 'shelf'
    waitforgump 0xc0b1026d 5000
    // clear
    replygump 0xc0b1026d 16
    waitforgump 0xc0b1026d 5000
    // edit loadout
    replygump 0xc0b1026d 3
    waitforgump 0xc0b1026d 5000
    // cures (10)
    replygump 0xc0b1026d 201
    waitforgump 0xc0b1026d 5000
    replygump 0xc0b1026d 216
    waitforgump 0xc0b1026d 5000
    // heals (10)
    replygump 0xc0b1026d 200
    waitforgump 0xc0b1026d 15000
    replygump 0xc0b1026d 215
    waitforgump 0xc0b1026d 15000
    //
    // 78 poison
    //
    if skill 'Poisoning' <= 78
      // select greater
      replygump 0xc0b1026d 207
      waitforgump 0xc0b1026d 5000
      // load 10
      for 10
        replygump 0xc0b1026d 222
        waitforgump 0xc0b1026d 5000
      endfor
    endif
    //
    // 100 poison
    //
    if skill 'Poisoning' <= 98 and skill 'Poisoning' > 78
      // select greater
      replygump 0xc0b1026d 208
      waitforgump 0xc0b1026d 5000
      // load 10
      for 10
        replygump 0xc0b1026d 223
        waitforgump 0xc0b1026d 5000
      endfor
    endif
    //
    // 120 poison
    //
    if skill 'Poisoning' <= 120 and skill 'Poisoning' > 98
      // select lethal
      replygump 0xc0b1026d 209
      waitforgump 0xc0b1026d 5000
      // load 10
      for 10
        replygump 0xc0b1026d 224
        waitforgump 0xc0b1026d 5000
      endfor
    endif
    //
    // resupply
    //
    replygump 0xc0b1026d 7
    waitforgump 0xc0b1026d 5000
    pause 2000
    replygump 0xc0b1026d 0
    //
    // resupply cure check
    //
    if @counttype 0xf07 'any' 'backpack' < 10
      headmsg "Failed to restock 10 cures!" 73
      playsound 'uhoh.wav'
      pause 5000
      continue
    endif
    //
    // heals check
    //
    if @counttype 0xf0c 'any' 'backpack' < 10
      headmsg "Failed to restock 10 heals!" 73
      playsound 'uhoh.wav'
      pause 5000
      continue
    endif
    //
    // poison check
    //
    if @counttype 0xf0a 'any' 'backpack' < 10
      headmsg "Failed to restock 10 poisons!" 73
      playsound 'uhoh.wav'
      pause 5000
      continue
    endif
  endif
  //
  // Check : Health or drink heal
  //
  if diffhits > 10
    @usetype 0xf0c 'any' 'backpack'
    pause 2000
    continue
  endif
  //
  // Server save
  //
  if @injournal 'will save' 'system'
    sysmsg "Server save..." 67
    pause 30000
    @clearjournal
  endif
  //
  // Poison
  //
  sysmsg "Applying poison..." 67
  @clearjournal
  @findtype 0xf0a 'any' 'backpack'
  useskill 'Poisoning'
  waitfortarget 5000
  target! 'found'
  //
  // kryss
  //
  @findtype 0x1401 'any' 'backpack'
  waitfortarget 5000
  target! 'found'
  //
  // Wait for result
  //
  @removetimer 'apply'
  @createtimer 'apply'
  while timer 'apply' < 10000
    if @injournal 'grave mistake' 'system'
      break
    elseif @injournal "apply" "system"
      break
    endif
    pause 100
  endwhile
  //
  // Poisoned?
  //
  while poisoned
    playsound 'uhoh.wav'
    headmsg 'Darnit!' 55
    usetype 0xf07 'any' 'backpack'
    pause 2000
  endwhile
  pause 5000
  //
  // Extra failsafe poison check
  //
  while poisoned
    headmsg 'Darnit!' 55
    usetype 0xf07 'any' 'backpack'
    pause 2000
  endwhile
endwhile
