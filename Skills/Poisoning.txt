/////////////////////////////////////////////
// UO Steam Poisoning Trainer
// for Outlands
// by Jewele
//
// How to use?
// -----------
// 1) Start at bank or home
// 2) Modify script for POISON you want to use
// 3) Have 1 kryss in your pack
// 4) Place in a bag or container (at home or bank):
//    1 or more kegs of Poison appropriate to your skill
//    1 or more Greater Cure kegs
// 5) Have some empty bottles in your pack (10+)
// 6) Stock heal regs in pack (or bandages)
// 7) Run script
//
// Bank or Home?
// -------------
// You can use the script from home if you wish,
// just select the container in your house instead
// of a bank bag.
//
// Notes
// -----
// I know you can use a keg directly from a container
// without transferring to your pack first. However,
// some folks may prefer emptying a keg first before
// tapping a new keg. By placing less than full poison
// kegs in their pack to start, they will be used first.
//
// Sounds
// ------
// ..\UOSteam\Sounds\UhOh.wav
// ..\UOSteam\Sounds\Cymbals.wav
//
// Wiki Info:
// ----------
// Lesser Poison: 0-40 Poisoning
// Regular Poison: 35-60 Poisoning
// Greater Poison: 55-80 Poisoning
// Deadly Poison: 75-100 Poisoning
// Lethal Poison: 95-120 Poisoning
//
// Versions
// --------
// 2.1 - Minor fixes to handle errors
//       Added sound file support
//
// UOSTEAM COPY-PASTE BELOW THIS LINE
/////////////////////////////////////////////
sysmsg "Poisoning Trainer Script" 73
sysmsg "Version 2.1" 73
//
// Open backpack
//
@useobject 'backpack'
pause 1000
//
// Set alias'
//
if not findalias 'kegbag'
  sysmsg "Select bag with poison and cure kegs" 73
  promptalias 'kegbag'
endif
//
// Open bank
//
msg "bank"
pause 1000
msg "bank"
pause 1000
//
// Check for empty bottles
//
if @counttype 0xf0e 0 'backpack' < 3
  headmsg "I need empty bottles!" 73
  playsound 'uhoh.wav'
  pause 5000
  continue
endif
@clearjournal
//
// Main loop
//
while not dead
  //
  // Check for a poison keg or transfer one
  //
  if not @findtype 0x1940 363 'backpack'
    movetype 0x1940 'kegbag' 'backpack' 0 0 0 363 1
    pause 2500
  endif
  //
  // Verify keg
  //
  if not @findtype 0x1940 363 'backpack'
    headmsg "I need a poison keg to continue!" 73
    playsound 'uhoh.wav'
    pause 5000
    continue
  endif
  //
  // Move empty kegs back
  //
  if @findtype 0x1940 0 'backpack'
    movetype 0x1940 'backpack' 'kegbag' 0 0 0 0 1
    pause 2000
  endif
  //
  // Use keg to fill poison bottle
  //
  if not @findtype 0xf0a 'any' 'backpack'
    usetype 0x1940 363 'backpack'
    pause 2500
  endif
  //
  // Check poison
  //
  if not @findtype 0xf0a 'any' 'backpack'
    headmsg "I need a poison potion!" 73
    playsound 'uhoh.wav'
    pause 5000
    continue
  endif
  //
  // Check cures
  //
  settimer 'restock' 0
  while @counttype 0xf07 'any' 'backpack' < 5 and timer 'restock' < 10000
    @findtype 0x1940 44 'kegbag'
    sysmsg "Refilling cure potions" 44
    @useobject 'found'
    pause 5000
  endwhile
  //
  // Verify cures
  //
  if @counttype 0xf07 'any' 'backpack' < 5
    headmsg "I need more Cure potions! (or empty bottles)" 91
    playsound 'uhoh.wav'
    pause 5000
    continue
  endif
  @clearjournal
  //
  // Poison
  //
  @findtype 0xf0a 'any' 'backpack'
  useskill 'Poisoning'
  waitfortarget 5000
  target! 'found'
  //
  // kryss
  //
  @findtype 0x1401 'any' 'backpack'
  waitfortarget 5000
  target! 'found'
  pause 2000
  //
  // Poisoned?
  //
  while poisoned
    headmsg 'Darnit!' 55
    playsound 'uhoh.wav'
    usetype 0xf07 'any' 'backpack'
    pause 2000
    if not findtype 0xf07 'any' 'backpack'
      break
    endif
  endwhile
  //
  // Heal
  //
  while diffhits > 20
    if skill 'magery' > 60
      bigheal 'self'
      pause 2000
    endif
    if skill 'healing' > 60
      bandageself!
      pause 20000
    endif
    pause 5000
  endwhile
  //
  // Skill cooldown
  //
  pause 3000
  //
  // World save check
  //
  if @injournal 'world will save' 'system'
    sysmsg 'pausing for world save'
    pause 30000
  endif
  @clearjournal
endwhile